(require 'yaml)
(require 'dash)
(require 'color)

(defun hex-color (x)
  (concat "#"
          (cond ((stringp x) x)
                ((numberp x) (number-to-string x)))))

(defmacro with-base-colors (table &rest body)
  `(let ,(mapcar #'(lambda (key)
                     `(,key (hex-color
                             (gethash (intern-soft
                                       ,(concat "base"
                                                (upcase (symbol-name key))))
                                      ,table))))
                 '(a0 a1 a2 a3 a4 a5 a6 a7
                   b0 b1 b2 b3 b4 b5 b6 b7
                   c0 c1 c2 c3 c4 c5 c6 c7
                   d0 d1 d2 d3 d4 d5 d6 d7))
     ,@body))

(defun hex-to-rgb (str)
  (--map (/ (string-to-number it 16) 255.0)
         (seq-partition (substring str 1) 2)))

(defun rgb-to-hex (rgb)
  (apply (-on (-partial #'format "#%02x%02x%02x")
              (-partial #'* 255))
         rgb))

(defun edit-in-hsl (fn hex)
  (->> hex
       (hex-to-rgb)
       (apply #'color-rgb-to-hsl)
       (apply fn)
       (apply #'color-hsl-to-rgb)
       (rgb-to-hex)))

(defun lighten (d hex)
  (edit-in-hsl (-cut color-lighten-hsl <> <> <> (* 100 d))
               hex))

(defun darken (d hex)
  (lighten (* -1 d) hex))

(defun generate-theme (config-path)
  (with-temp-buffer
    (insert-file-contents config-path)
    (let* ((c (yaml-parse-string (buffer-string)))
           (theme-name (make-symbol (downcase (gethash 'scheme c)))))
      (with-base-colors c
        (pp `(deftheme ,theme-name))
        (terpri)
        (pp `(custom-theme-set-faces (quote ,theme-name)
              '(default ((((type tty))) 
                         (t (:background ,a0 :foreground ,a7))))
              '(cursor ((t (:background ,d5))))
	      '(success ((t (:foreground ,d6))))
              '(error ((t (:foreground ,d2 :weight bold))))
              '(link ((t (:foreground ,d6 :underline t))))
	      '(link-visited ((t (:inherit link :foreground ,d2))))
              '(button ((t (:inherit link :underline nil))))
              '(region ((t (:background ,a1))))
              '(secondary-selection ((t (:background ,b0))))
	      '(highlight ((t (:background ,a1))))
              '(lazy-highlight ((t (:background ,a2))))
              '(match ((t (:foreground ,d4 :underline t))))
	      '(line-number ((t (:foreground ,a2))))
	      '(line-number-current-line ((t (:foreground ,a3))))
              '(window-divider ((t (:background ,(darken 0.2 a0)
                                    :foreground ,(darken 0.2 a0)))))
              '(vertical-border ((t (:inherit window-divider))))
              '(tooltip ((t (:background ,d0 :foreground ,a0))))
              '(widget-inactive ((t (:foreground ,a2))))
              '(widget-field ((t (:background ,a0 :foreground ,c7))))
              '(help-key-binding ((t (:background ,a0 :foreground ,d0))))
              '(completions-common-part ((t (:foreground ,d7))))
              '(meow-position-highlight-number ((t (:background ,a1 :foreground ,d2))))

              '(ansi-color-black ((t (:background ,a0 :foreground ,a0))))
              '(ansi-color-red ((t (:background ,b2 :foreground ,b2))))
              '(ansi-color-green ((t (:background ,d4 :foreground ,d4))))
              '(ansi-color-yellow ((t (:background ,d7 :foreground ,d7))))
              '(ansi-color-blue ((t (:background ,b3 :foreground ,b3))))
              '(ansi-color-magenta ((t (:background ,d4 :foreground ,d4))))
              '(ansi-color-cyan ((t (:background ,b4 :foreground ,b4))))
              '(ansi-color-white ((t (:background ,a7 :foreground ,a7))))
              '(ansi-color-bright-black ((t (:background ,a3 :foreground ,a3))))
              '(ansi-color-bright-red ((t (:background ,d5 :foreground ,d5))))
              '(ansi-color-bright-green ((t (:background ,d4 :foreground ,d4))))
              '(ansi-color-bright-yellow ((t (:background ,d7 :foreground ,d7))))
              '(ansi-color-bright-blue ((t (:background ,b3 :foreground ,b3))))
              '(ansi-color-bright-magenta ((t (:background ,d4 :foreground ,d4))))
              '(ansi-color-bright-cyan ((t (:background ,d3 :foreground ,d3))))
              '(ansi-color-bright-white ((t (:background ,b7 :foreground ,b7))))

              '(tab-bar ((t (:background ,a1))))
              '(tab-bar-tab ((t (:background ,a2))))
              '(tab-bar-tab-group-current ((t (:background ,a2 :weight bold))))
              '(tab-bar-tab-group-inactive ((t (:background ,a2))))
              '(tab-bar-tab-inactive ((t (:background ,a1))))
              '(tab-bar-tab-ungrouped ((t (:background ,a1))))
              '(tab-line ((t (:background ,a1))))

              '(tty-menu-disabled-face ((t (:background ,a0 :foreground ,c5))))
              '(tty-menu-enabled-face ((t (:inherit tty-menu-disabled-face :foreground ,d7))))
              '(tty-menu-selected-face ((t (:background ,a1))))

              '(font-lock-builtin-face ((t (:foreground ,b2))))
              '(font-lock-comment-face ((t (:foreground ,a3 :slant italic))))
              '(font-lock-doc-face ((t (:inherit font-lock-comment-face))))
              '(font-lock-constant-face ((t (:foreground ,d7))))
              '(font-lock-number-face ((t (:foreground ,d7))))
              '(font-lock-operator-face ((t (:foreground ,d2))))
              '(font-lock-function-name-face ((t (:foreground ,b7))))
              '(font-lock-variable-name-face ((t (:foreground ,b7))))
              '(font-lock-keyword-face ((t (:foreground ,d2))))
              '(font-lock-string-face ((t (:foreground ,d5))))
              '(font-lock-type-face ((t (:foreground ,d7 :slant italic))))
              '(font-lock-variable-face ((t (:foreground ,b5))))
              '(sh-heredoc ((t (:foreground ,d5))))

              '(header-line ((t (:background ,a1 :foreground ,d5 :weight bold))))
              '(magit-branch-current ((t (:background ,b3 :foreground ,a0))))
              '(magit-branch-local ((t (:foreground ,b3))))
              '(magit-branch-remote ((t (:foreground ,d0))))
              '(magit-branch-remote-head ((t (:background ,d0 :foreground ,a0))))
              '(magit-branch-warning ((t (:foreground ,d3 :slant italic))))
              '(magit-head ((t (:foreground ,b3))))
              '(magit-header-line ((t (:foreground ,d5 :weight bold))))
              '(magit-section-heading ((t (:foreground ,d5 :weight bold))))
              '(magit-section-heading-select ((t (:inherit magit-section-heading :background ,a1))))

              '(company-tooltip ((t (:background ,a0 :foreground ,c5))))
              '(company-tooltip-annotation ((t (:foreground ,a2))))
              '(company-tooltip-common ((t (:foreground ,d7))))
              '(company-tooltip-selection ((t (:background ,a1))))
              '(company-tooltip-scrollbar-thumb ((t (:background ,d0))))
              '(company-tooltip-scrollbar-track ((t (:background ,a0))))
              '(flycheck-error ((t (:underline t))))
              '(lsp-flycheck-warning-unnecessary-face ((t (:underline t))))
              '(lsp-lens-face ((t (:foreground ,a3))))

              '(nix-constant-face ((t (:foreground ,d5))))
              '(markdown-header-face ((t (:foreground ,d5 :weight bold))))

              '(org-document-title ((t (:foreground ,d5 :weight bold))))
              '(org-footnote ((t (:foreground ,d7 :slant italic))))

	      '(minibuffer-prompt ((t (:foreground ,d0))))
	      '(show-paren-match ((t (:background ,a0 :foreground ,d4 :underline t))))
              '(show-paren-mismatch ((t (:background ,d2 :foreground ,a0))))))
        (terpri)
        (pp `(provide-theme (quote ,theme-name)))))))
